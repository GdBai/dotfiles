" G. Bai's .vimrc

let mapleader = " "
let maplocalleader = " "
if has('win32')
  set nocompatible " Get out of vi compatible mode
endif
let system_name = substitute(system('uname'), '\n', '', '') " Reserve for later

" Load local .vimrc files
set exrc
set secure

if !exists("*ReloadConfigs")
  function ReloadConfigs()
    :source ~/.vimrc
    if has('gui_running')
      if !has('gui_vimr')
        :source ~/.gvimrc
      else
        :source ~/.config/nvim/ginit.vim
      endif
    endif
  endfunction
  command! RC call ReloadConfigs()
endif

set splitbelow splitright " Split new window below and right
set laststatus=2 " Always show status line
set wildmenu " Enable a menu at the bottom with tab-key completion
set showtabline=2 " Show tabline
set ruler " Show current cursor position
set nu " Switch on numbering
set encoding=utf-8 " Necessary for Windows to display vim-airline properly
set fileencodings=ucs-bom,utf-8,utf-16,gbk,big5,gb18030,latin1
set hlsearch " Highlight search result
set incsearch " See cursor move when typing
set mouse=a " Enable use of mouse for all modes
set linebreak " Wrap lines at characters in 'breakat'
set display+=lastline " Show last line in the window rather than '@'
set maxmempattern=2000000 " Avoid E363
set autochdir " For Neovim
if $TERM_PROGRAM !=# 'Apple_Terminal'
  set termguicolors " For colorizer.lua
  let &t_8f = "\<Esc>[38;2;%lu;%lu;%lum"
  let &t_8b = "\<Esc>[48;2;%lu;%lu;%lum"
endif
if has('nvim')
  set shortmess+=S
  set inccommand=nosplit " See :substitute command change the document live
else
  set ttymouse=sgr " For Alacritty terminfo
endif
set noeb vb t_vb= " Use visualbell instead of beep sound, also avoid change in tmuxline

" Time out
set timeoutlen=500
set ttimeout
set ttimeoutlen=100

" Cursor
if has('nvim')
  augroup reset_nvim_cursor
    autocmd!
    autocmd VimEnter,VimResume * set guicursor=n-v-c-sm:block-blinkon100,i-ci-ve:ver25-blinkon100,r-cr-o:hor20-blinkon100
    autocmd VimLeave,VimSuspend * set guicursor=a:ver25-blinkon100
  augroup END
else
  if $TERM_PROGRAM ==# 'iTerm.app'
    let &t_SI = "\<Esc>]50;CursorShape=1;BlinkingCursorEnabled=1\x7"
    let &t_SR = "\<Esc>]50;CursorShape=2;BlinkingCursorEnabled=1\x7"
    let &t_EI = "\<Esc>]50;CursorShape=0;BlinkingCursorEnabled=1\x7"
    " Initialize cursor shape/color on startup
    augroup reset_iTerm_cursor_on_enter
      autocmd!
      "autocmd VimEnter * startinsert | stopinsert
      autocmd VimEnter * normal! :startinsert :stopinsert
      "autocmd VimEnter * :normal :startinsert :stopinsert
      autocmd VimEnter * redraw!
    augroup END
  else
    let &t_SI .= "\e[5 q" " SI = INSERT mode
    let &t_SR .= "\e[3 q" " SR = REPLACE mode
    let &t_EI .= "\e[1 q" " EI = NORMAL mode (ELSE)
    " Initialize cursor shape/color on startup
    augroup reset_cursor_on_enter
      autocmd!
      "autocmd VimEnter * startinsert | stopinsert
      autocmd VimEnter * normal! :startinsert :stopinsert
      "autocmd VimEnter * :normal :startinsert :stopinsert
      autocmd VimEnter * redraw!
    augroup END
  endif
  " Reset cursor when Vim exits:
  augroup reset_cursor_on_leave
    autocmd!
    autocmd VimLeave * silent !echo -ne "\033[5 q"
  augroup END
endif

" Spell
set spelllang=en_gb
augroup spell_setting
  autocmd!
  autocmd FileType tex setlocal spell
augroup END
let g:tex_comment_nospell = 1 " For vimtex use g:vimtex_syntax_nospell_comments

" Toggle relative numbering
nnoremap <C-n> :set rnu!<CR>

" Color scheme
syntax enable
colorscheme slate
highlight Normal ctermbg=235
highlight StatusLine ctermfg=253
highlight StatusLineNC ctermfg=243
highlight Tabline ctermbg=239
highlight TablineFill ctermfg=243
highlight VertSplit ctermfg=235 ctermbg=243

" Prevent wrong bg color during rapid scrolling
if (&term =~ '^xterm' && &t_Co == 256)
  set t_ut= | set ttyscroll=1
endif

augroup highlighting_fix
  autocmd!
  " fix vim script user command syntax highlighting
  " (should be unnecessary when https://github.com/vim/vim/issues/6587 is fixed)
  autocmd Syntax vim syn match vimUsrCmd '^\s*\zs\u\%(\w*\)\@>(\@!'
augroup END

" Indentation and tab
filetype indent on
set expandtab " Always use space instead of tab
augroup tab_setting
  autocmd!
  autocmd FileType vim setlocal expandtab tabstop=2 shiftwidth=2 softtabstop=2
  autocmd FileType sh setlocal expandtab tabstop=2 shiftwidth=2 softtabstop=2
  autocmd FileType bash setlocal expandtab tabstop=2 shiftwidth=2 softtabstop=2
  autocmd FileType zsh setlocal expandtab tabstop=2 shiftwidth=2 softtabstop=2
  autocmd FileType python setlocal expandtab tabstop=4 shiftwidth=4 softtabstop=4
  autocmd FileType tex setlocal expandtab tabstop=2 shiftwidth=2 softtabstop=2
  autocmd FileType markdown setlocal expandtab tabstop=4 shiftwidth=4 softtabstop=4
  autocmd FileType json setlocal expandtab tabstop=2 shiftwidth=2 softtabstop=2
  autocmd FileType yaml setlocal expandtab tabstop=2 shiftwidth=2 softtabstop=2
augroup END

" Clipboard
if system_name ==# "Darwin"
  set clipboard=unnamed
  noremap <Leader>y "*y
  noremap <Leader>p "*p
elseif system_name ==# "Linux"
  set clipboard=unnamedplus
  noremap <Leader>y "+y
  noremap <Leader>p "+p
  if has('nvim')
    let g:clipboard = {
          \   'name': 'xsel_override',
          \   'copy': {
          \      '+': 'xsel --input --clipboard',
          \      '*': 'xsel --input --primary',
          \    },
          \   'paste': {
          \      '+': 'xsel --output --clipboard',
          \      '*': 'xsel --output --primary',
          \   },
          \   'cache_enabled': 1,
          \ }
  endif
endif

" Paste toggle
nnoremap <C-p> :set invpaste paste?<CR>
set pastetoggle=<C-p>

" Buffer
" Close the current buffer and move to the previous one
" This replicates the idea of closing a tab
nnoremap <leader>bd :bp! \| bd #<CR>
" A function to skip QuickFix buffer
function! BSkipQuickFix(command)
  let start_buffer = bufnr('%')
  execute a:command
  while &buftype ==# 'quickfix' && bufnr('%') != start_buffer
    execute a:command
  endwhile
endfunction
" Move to next buffer
nnoremap <leader>bn :call BSkipQuickFix("bn!")<CR>
" Move to previous buffer
nnoremap <leader>bp :call BSkipQuickFix("bp!")<CR>

" Window shortcuts
nnoremap <C-j> <C-w>j
nnoremap <C-k> <C-w>k
nnoremap <C-h> <C-w>h
nnoremap <C-l> <C-w>l

augroup quickfix_window_height
  autocmd!
  autocmd FileType qf call AdjustWindowHeight(3, 10)
augroup END
function! AdjustWindowHeight(minheight, maxheight)
  exe max([min([line("$"), a:maxheight]), a:minheight]) . "wincmd _"
endfunction

" Line number from 0 shortcuts. NE: ein; NA: aus
command! NE set nonumber | %s/^/\=line('.')-1."\t"/ | noh
command! NA %s/^[^\t]*\t// | set number | noh

if system_name ==# "Darwin"
  if has('nvim')
    let g:python_host_prog = '$HOME/.pyenv/versions/py2nvim/bin/python'
    let g:python3_host_prog = '$HOME/.pyenv/versions/py3nvim/bin/python'
  elseif has('python3_dynamic')
    "set pythonhome=$HOME/.pyenv/versions/2.7.18
    "set pythondll=$HOME/.pyenv/versions/2.7.18/lib/libpython2.7.dylib
    "set pythonthreehome=$HOME/.pyenv/versions/3.8.10
    "set pythonthreedll=$HOME/.pyenv/versions/3.8.10/lib/libpython3.8.dylib
    py3 1+2
    pyx 1+2
  endif
elseif system_name ==# "Linux"
  if has('nvim')
    let g:python3_host_prog = '$HOME/miniforge3/envs/py3nvim/bin/python'
  endif
endif

" Check if memory size is large enough to use plugins
if system_name ==# "Darwin"
  let flag = "good"
elseif system_name ==# "Linux"
  let mem_size = system("free -mt | tail -1 | awk '{print $2}'") " Get memory size in MB
  if mem_size > 3600
    let flag = "good"
  else
    let flag = "nogood"
  endif
endif

" Windows also works
if has('win32')
  let flag = "good"
endif

if flag == "nogood"
  " Auto source .vimrc after saving
  augroup auto_source_vimrc
    autocmd!
    autocmd BufWritePost $MYVIMRC source $MYVIMRC
  augroup END
elseif flag == "good"
  " Auto source .vimrc and refresh vim-airline after saving
  augroup auto_source_vimrc_refresh_airline
    autocmd!
    autocmd BufWritePost $MYVIMRC source $MYVIMRC | call airline#extensions#tabline#buffers#invalidate() | AirlineRefresh
  augroup END

  " vim-plug
  " Automatic installation
  "let data_dir = has('nvim') ? stdpath('data') . '/site' : '~/.vim'
  "if empty(glob(data_dir . '/autoload/plug.vim'))
  "  silent execute '!curl -fLo '.data_dir.'/autoload/plug.vim --create-dirs  https://raw.githubusercontent.com/junegunn/vim-plug/master/plug.vim'
  "  autocmd VimEnter * PlugInstall --sync | source $MYVIMRC
  "endif

  " vim-plug conditional activation
  function! Cond(cond, ...)
    let opts = get(a:000, 0, {})
    return a:cond ? opts : extend(opts, {'on': [], 'for': []})
  endfunction

  call plug#begin('~/.vim/plugged')
  Plug 'tpope/vim-fugitive'
  Plug 'vim-airline/vim-airline' | Plug 'vim-airline/vim-airline-themes'
  Plug 'nathanaelkane/vim-indent-guides'
  Plug 'morhetz/gruvbox'
  "Plug 'cormacrelf/vim-colors-github'
  Plug 'preservim/nerdtree'
  Plug 'Xuyuanp/nerdtree-git-plugin'
  Plug 'edkolev/tmuxline.vim' " See tmuxline.vim settings section
  Plug 'lervag/vimtex'
  Plug 'godlygeek/tabular' " tabular plugin must come before vim-markdown
  Plug 'plasticboy/vim-markdown' | Plug 'JamshedVesuna/vim-markdown-preview'
  Plug 'SirVer/ultisnips' | Plug 'honza/vim-snippets'
  Plug 'psliwka/vim-smoothie'
  Plug 'skywind3000/Leaderf-snippet'
  Plug 'mhinz/vim-startify'
  Plug 'jdhao/better-escape.vim'
  Plug 'kkoomen/vim-doge', { 'do': { -> doge#install() } }
  Plug 'ryanoasis/vim-devicons'
  Plug 'Yggdroot/LeaderF', Cond(!has('nvim-0.5'), {'do': ':LeaderfInstallCExtension'})
  Plug 'neoclide/coc.nvim', Cond(has('nvim'))
  Plug 'norcalli/nvim-colorizer.lua', Cond(has('nvim'))
  Plug 'nvim-lua/popup.nvim', Cond(has('nvim'))
  Plug 'nvim-lua/plenary.nvim', Cond(has('nvim'))
  Plug 'nvim-treesitter/nvim-treesitter', Cond(has('nvim-0.5'), {'do': ':TSUpdate'})
  Plug 'nvim-telescope/telescope.nvim', Cond(has('nvim-0.5'))
  Plug 'kyazdani42/nvim-web-devicons', Cond(has('nvim')) " For telescope.nvim
  Plug 'tmsvg/pear-tree'
  Plug 'Rykka/riv.vim'
  Plug 'rhysd/committia.vim'
  call plug#end()

  " gruvbox
  let g:gruvbox_contrast_dark = 'hard'
  let g:gruvbox_contrast_light = 'hard'
  colorscheme gruvbox

  function! DarkIndentGuides()
    "highlight IndentGuidesOdd  ctermbg=242 guibg=#696969
    "highlight IndentGuidesEven ctermbg=008 guibg=#808080
    " Below are from gruvbox palette
    highlight IndentGuidesOdd  ctermbg=239 guibg=#504945 " bg2
    highlight IndentGuidesEven ctermbg=241 guibg=#665c54 " bg3
  endfunction

  function! LightIndentGuides()
    "highlight IndentGuidesOdd  ctermbg=252 guibg=#d3d3d3
    "highlight IndentGuidesEven ctermbg=250 guibg=#bebebe
    " Below are from gruvbox palette
    highlight IndentGuidesOdd  ctermbg=228 guibg=#f2e5bc " bg0_s
    highlight IndentGuidesEven ctermbg=223 guibg=#ebdbb2 " bg1
  endfunction

  function! DarkColorscheme()
    set background=dark
    colorscheme gruvbox
    let g:airline_theme = "gruvbox"
  endfunction

  function! LightColorscheme()
    set background=light
    colorscheme gruvbox
    let g:airline_theme = "gruvbox"
  endfunction

  " IndentGuides(): Set Indent Guides color
  function! IndentGuides(background)
    if a:background == "dark"
      call DarkIndentGuides()
    elseif a:background == "light"
      call LightIndentGuides()
    endif
  endfunction

  " ToggleBackground(): Toggle color scheme
  function! ToggleBackground()
    if &background == "dark"
      call LightColorscheme()
      call IndentGuides(&background)
    elseif &background == "light"
      call DarkColorscheme()
      call IndentGuides(&background)
    endif
    call airline#extensions#tabline#buffers#invalidate()
    AirlineRefresh
    if has('nvim')
      lua package.loaded['colorizer'] = nil; require('colorizer').setup(...); require('colorizer').attach_to_buffer(0)
    endif
    if has('gui_vimr')
      VimRToggleTools
      VimRToggleTools
    endif
    highlight Comment cterm=italic gui=italic
  endfunction

  nmap tb :call ToggleBackground()<CR>

  " MacBackground(): Set color scheme for macOS
  function! MacBackground()
    if system("defaults read -g AppleInterfaceStyle") =~ "^Dark"
      call DarkColorscheme()
      call IndentGuides(&background)
    else
      call LightColorscheme()
      call IndentGuides(&background)
    endif
    highlight Comment cterm=italic gui=italic
  endfunction

  " MacGUIBackground(): change MacVim gVim color scheme automatically
  function! MacGUIBackground()
    if v:os_appearance == 1
      call DarkColorscheme()
      call IndentGuides(&background)
    else
      call LightColorscheme()
      call IndentGuides(&background)
    endif
    call airline#extensions#tabline#buffers#invalidate()
    AirlineRefresh
    highlight Comment cterm=italic gui=italic
  endfunction

  if system_name ==# "Darwin"
    if !has('nvim')
      call MacBackground()
      augroup MacVim_auto_background
        autocmd!
        autocmd OSAppearanceChanged * call MacGUIBackground()
      augroup END
    else
      call MacBackground()
    endif
  endif

  " Indent Guides
  let g:indent_guides_enable_on_vim_startup = 1
  let g:indent_guides_auto_colors = 0
  if &background == 'dark'
    augroup dark_indent_guides
      autocmd!
      autocmd VimEnter,Colorscheme * :call DarkIndentGuides()
    augroup END
  elseif &background == 'light'
    augroup light_indent_guides
      autocmd!
      autocmd VimEnter,Colorscheme * :call LightIndentGuides()
    augroup END
  endif
  let g:indent_guides_start_level = 1
  let g:indent_guides_color_change_percent = 100
  let g:indent_guides_guide_size = 1
  nmap <silent> <leader>i <Plug>IndentGuidesToggle

  " NERDTree
  let NERDTreeShowHidden = 1
  let NERDTreeMinimalUI = 1
  command! NT NERDTreeToggle
  "command! NT NERDTreeToggle | AirlineRefresh
  nmap <leader>nt :NT<CR>
  augroup NERDTree_buffer_refresh
    autocmd!
    autocmd BufEnter * if (winnr("$") == 1 && exists("b:NERDTree") && b:NERDTree.isTabTree()) | q | endif
    " If more than one window and previous buffer was NERDTree, go back to it.
    autocmd BufEnter * if bufname('#') =~# "^NERD_tree_" && winnr('$') > 1 | b# | endif
  augroup END

  " vim-airline
  let g:airline_powerline_fonts = 1
  let g:airline#extensions#tabline#enabled = 1
  " Enable next two lines to define straight tabs
  "let g:airline#extensions#tabline#left_sep = ' '
  "let g:airline#extensions#tabline#left_alt_sep = '|'
  let g:airline#extensions#tabline#formatter = 'unique_tail_improved'
  " Insert separator between whitespace warning and coc warning
  if has('nvim-0.5')
    let g:airline_section_warning = '%{airline#util#prepend(airline#extensions#nvimlsp#get_warning(),0)}%{airline#util#prepend(airline#extensions#whitespace#check(),0)}%{airline#util#wrap(airline#extensions#coc#get_warning(),0)}'
  else
    let g:airline_section_warning = '%{airline#util#prepend(airline#extensions#whitespace#check(),0)}%{airline#util#wrap(airline#extensions#coc#get_warning(),0)}'
  endif

  " Workaround for display problem in kitty SSH session
  if $TERM == 'xterm-kitty'
    augroup airline_init_kitty
      autocmd!
      autocmd VimEnter * call airline#extensions#tabline#buffers#invalidate()
      autocmd VimEnter * AirlineRefresh
      autocmd VimEnter * redraw!
    augroup END
  endif

  " tmuxline.vim
  " Run :Tmuxline then :TmuxlineSnapshot! [file]
  " (usually need this '!' to override)
  function! TmuxlineThemeSnapshot(theme)
    execute "Tmuxline ". a:theme
    TmuxlineSnapshot! ~/.config/tmux/tmuxline.snapshot
  endfunction
  command! -nargs=1 TTS call TmuxlineThemeSnapshot(<f-args>)
  let g:airline#extensions#tmuxline#enabled = 1
  "let airline#extensions#tmuxline#snapshot_file = "~/.tmuxline.snapshot"
  let g:tmuxline_preset = {
    \'a'    : '#S',
    \'win'  : '#I #W',
    \'cwin' : '#I #W',
    \'x'    : '%d-%b',
    \'y'    : '%R',
    \'z'    : '#H'}
  augroup reset_tmuxline
    autocmd!
    autocmd VimLeave * !tmux if-shell "test -f ~/.config/tmux/tmuxline.snapshot" "source ~/.config/tmux/tmuxline.snapshot" Enter
  augroup END
  augroup update_tmuxline
    autocmd!
    if exists('$TMUX')
      function! UpdateTmuxlineByMode()
        let current_mode = mode()
        if current_mode ==# 'i'
          Tmuxline airline_insert
        elseif current_mode ==# 'v'
          Tmuxline airline_visual
        elseif current_mode ==# 'V'
          Tmuxline airline_visual
        elseif current_mode ==# 'CTRL-V'
          Tmuxline airline_visual
        else
          Tmuxline airline
        endif
      endfunction
      function! UpdateTmuxlineVisualEnter()
        set updatetime=0
        call UpdateTmuxlineByMode()
        return ''
      endfunction
      function! UpdateTmuxlineVisualLeave()
        set updatetime=4000
        call UpdateTmuxlineByMode()
      endfunction
      autocmd FocusGained * call UpdateTmuxlineByMode()
      autocmd InsertEnter * Tmuxline airline_insert
      autocmd InsertLeave * Tmuxline airline
      vnoremap <silent> <expr> <SID>UpdateTmuxlineVisualEnter UpdateTmuxlineVisualEnter()
      nnoremap <silent> <script> v v<SID>UpdateTmuxlineVisualEnter<left><right>
      nnoremap <silent> <script> V V<SID>UpdateTmuxlineVisualEnter<left><right>
      nnoremap <silent> <script> <C-v> <C-v><SID>UpdateTmuxlineVisualEnter<left><right>
      autocmd CursorHold * call UpdateTmuxlineVisualLeave()
    endif
  augroup END

  " VimTeX
  let g:tex_flavor = 'latex'
  let g:vimtex_compiler_latexmk_engines = {'_': '-xelatex'}
  let g:vimtex_view_method = 'skim'
  let g:vimtex_mappings_enabled = 1
  let g:vimtex_syntax_nospell_comments = 1
  let g:vimtex_grammar_vlty = {'lt_command': 'languagetool'}
  let g:vimtex_grammar_vlty.shell_options =
          \   ' --multi-language'
  let g:vimtex_grammar_textidote = {
          \ 'jar': '~/textidote.jar',
          \ 'args': '--check en',
          \}
  let g:airline#extensions#vimtex#main = "/"
  "if has('nvim')
  "  g:vimtex_compiler_progname = 'nvr'
  "endif
  command TexRoot silent exe 'cd' b:vimtex.root
  nmap <leader>l/ :TexRoot<CR>
  command TexReset silent exe ':VimtexStop' | exe ':VimtexClean' | exe '!rm' b:vimtex.root .. '/*.bbl'
  nmap <leader>ld :TexReset<CR>

  " vim-markdown-preview
  let vim_markdown_preview_github = 1 " if using grip
  let vim_markdown_preview_hotkey = '<C-m>'
  let vim_markdown_preview_browser = 'Google Chrome'

  " coc.nvim
  " Remap <CR> to make sure it confirms completion when popup menu is visible
  inoremap <expr> <CR> pumvisible() ? "\<C-y>" : "\<C-g>u\<CR>"
  " kitty config window needs node path to be set
  " when invoked by cmd+comma
  if system_name ==# "Darwin"
    let g:coc_node_path = '/usr/local/bin/node'
  elseif system_name ==# "Linux"
    let g:coc_node_path = '/usr/bin/node'
  endif
  " Add `:Format` command to format current buffer.
  command! -nargs=0 Format :call CocAction('format')

  " UltiSnips
  let g:UltiSnipsExpandTrigger = "<tab>"
  let g:UltiSnipsJumpForwardTrigger = "<tab>"
  let g:UltiSnipsJumpBackwardTrigger = "<s-tab>"
  let g:UltiSnipsSnippetStorageDirectoryForUltiSnipsEdit = '~/.vim/Ultisnips' " For Neovim rtp
  "let g:UltiSnipsEditSplit = "vertical"
  let g:UltiSnipsEnableSnipMate = 0

  " LeaderF
  if !has('nvim-0.5')
    let g:Lf_ShortcutF = "<leader>ff"
    let g:Lf_ShortcutB = "<leader>fb"
    let g:Lf_WindowPosition = 'popup'
    let g:Lf_PreviewInPopup = 1
    let g:Lf_StlSeparator = {'left': "\ue0b0", 'right': "\ue0b2"}
    let g:Lf_ShowDevIcons = 1
  endif

  " LeaderF-snippet
  inoremap <C-s> <C-\><C-o>:Leaderf snippet<CR>
  let g:Lf_PreviewResult = get(g:, 'Lf_PreviewResult', {})
  let g:Lf_PreviewResult.snippet = 1

  " colorizer.lua
  if $TERM_PROGRAM !=# 'Apple_Terminal' && has('nvim')
    lua require'colorizer'.setup()
  endif

  " vim-anzu
  " mapping
  " nmap n <Plug>(anzu-n-with-echo)
  " nmap N <Plug>(anzu-N-with-echo)
  "nmap * <Plug>(anzu-star-with-echo)
  "nmap # <Plug>(anzu-sharp-with-echo)
  " clear status
  "nmap <Esc><Esc> <Plug>(anzu-clear-search-status)
  " if start anzu-mode key mapping
  " anzu-mode is anzu(12/51) in screen
  "nmap n <Plug>(anzu-mode-n)
  "nmap N <Plug>(anzu-mode-N)

  " DoGe
  let g:doge_doc_standard_python = 'numpy'

  " nvim-treesitter
  if has('nvim-0.5')
    lua require'nvim-treesitter.configs'.setup { highlight = { enable = true } }
  endif

  " telescope.nvim
  if has('nvim-0.5')
    " Find files using Telescope command-line sugar.
    nnoremap <leader>ff <cmd>Telescope find_files<cr>
    nnoremap <leader>fg <cmd>Telescope live_grep<cr>
    nnoremap <leader>fb <cmd>Telescope buffers<cr>
    nnoremap <leader>fh <cmd>Telescope help_tags<cr>

    " Using lua functions
    "nnoremap <leader>ff <cmd>lua require('telescope.builtin').find_files()<cr>
    "nnoremap <leader>fg <cmd>lua require('telescope.builtin').live_grep()<cr>
    "nnoremap <leader>fb <cmd>lua require('telescope.builtin').buffers()<cr>
    "nnoremap <leader>fh <cmd>lua require('telescope.builtin').help_tags()<cr>
  endif
endif
